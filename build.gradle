repositories {
    mavenLocal()
    mavenCentral()
    // For spock
    maven {
        url 'http://oss.sonatype.org/content/repositories/releases/'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

ext.artifactId = 'flipside'
group = 'com.johnnywey'
version = '0.1.8-SNAPSHOT'

jar {
    baseName = artifactId
}

tasks.withType(JavaCompile) { 
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}   

sourceSets.main.groovy.srcDirs = ["src/main/groovy"]

groovydoc.groovyClasspath = configurations.default

idea.module {
    ext.gradleCacheVariable = 'GRADLE_CACHE'
    ext.downloadJavadoc = true
}

idea.project {
    ext.jdkName = '1.6'
    ext.wildcards = ['!?*.java', '!?*.groovy']
}

ext.groovy = 'org.codehaus.groovy:groovy-all:2.2.1'
ext.junit = 'junit:junit:4.11'
ext.spock = 'org.spockframework:spock-core:0.7-groovy-2.0'

dependencies {
    compile groovy
    testCompile junit
    testCompile spock
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

configurations {
    tests
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}    
  
signing {
    sign configurations.archives
}

test {
    String testType = System.properties['test.type']
    if (testType == 'integration') {
        include '**/*IntegrationTest.*'
        include '**/*IntegrationSpec.*'
    } else if (testType == 'unit') {
        include '**/*Test.*'
        include '**/*Spec.*'
        exclude '**/*IntegrationTest.*'
        exclude '**/*IntegrationSpec.*'
    } else if (testType == 'all') {
        include '**/*Test.*'
        include '**/*Spec.*'
    } else {
        //Default to unit
        include '**/*Test.*'
        include '**/*Spec.*'
        exclude '**/*IntegrationTest.*'
        exclude '**/*IntegrationSpec.*'
    }

    // Prints results of test inline
    afterSuite { desc, result ->
        // Don't report on modules with no test suites
        if (!desc.className) {
            return
        }

        String testStatus
        if (System.properties['nocolor']) {
            testStatus = result.resultType
        } else if (result.resultType != org.gradle.api.tasks.testing.TestResult.ResultType.SUCCESS) {
            // Print in red if failed
            testStatus = "\033[31m${result.resultType}\033[0m"
        } else {
            // Print in green if success/skipped
            testStatus = "\033[32m${result.resultType}\033[0m"
        }
        println "Test suite ${desc.name}: ${testStatus}"
    }
}

// Only *require* signing if we are uploading
gradle.taskGraph.whenReady { taskGraph ->
    tasks.withType(org.gradle.plugins.signing.Sign).all {
        required = taskGraph.hasTask(":uploadArchives")
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests :D'
    group = 'Verification'
    include '**/*IntegrationTest.*'
    include '**/*IntegrationSpec.*'
    afterSuite { desc, result ->
        if (desc.className)
            println "Test suite ${desc.name}: ${result.resultType}"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

def username = 'username'
def password = 'password'
if(hasProperty('sonatypeUsername')) {
    username = sonatypeUsername
}
if(hasProperty('sonatypePassword')) {
    password = sonatypePassword
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: username, password: password)
            }

            uniqueVersion = false

            pom.project {
                name 'Flipsde'
                packaging 'jar'
                description 'A simple Options library for Groovy'
                url 'https://github.com/johnnywey/flipside'

                scm {
                    url 'scm:git@github.com:johnnywey/flipside.git'
                    connection 'scm:git@github.com:johnnywey/flipside.git'
                    developerConnection 'scm:git@github.com:johnnywey/flipside.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

               developers {
                    developer {
                        id 'johnnywey'
                        name 'Johnny Wey'
                   }
               }
           }
        }
    }
}
